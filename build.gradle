buildscript {
    repositories {
        maven { url "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"

version = "${mod_version}-mc${mc_version}"
group = "com.terraforged"
archivesBaseName = "PreGenForge"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
}

minecraft {
    mappings channel: "${mcp_channel}", version: "${mcp_version}"
    runs {
        server {
            workingDirectory project.file("server")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            mods {
                pregenforge {
                    source sourceSets.main
                }
            }
        }
    }
}

processResources {
    filesMatching("**/mods.toml") {
        filter {
            it.replace("@version@", mod_version)
        }
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = "deobf"
}

jar {
    dependsOn(deobfJar)
    
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : project.name,
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : project.name,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}